#############################################################
#  Joint model JM1: two-change point model with random intercept and slopes
#############################################################

## Notation
##  M: number of individuals
##  i: identification of individual (i=1,2,...M)
##  k.pa: number of PA measurements for individual i
##  k.pe: number of PE measurements for individual i
##  j: index for measurement (j=1,2,...k.pa[i] or j=1,2,...k.pe[i])

##### Joint Model : shared random slope with random intercept
modelfixcp <- "
data { 
  for(i in 1:M){
       zeros[i]<- 0
  }
}
model { 
  for(i in 1:M){ 
        for(j in 1:k.pa[i]){
  ## PA: longitudinal sub-model
        Y[i,j] ~ dbin(p2[i,j],1)
        logit(p2[i,j]) <- c0 + (c[1]+u1[i]) * (X[i,j]-cp1) + (c[2]+u2[i]) * (X[i,j]-cp1) * (2*step(X[i,j]-cp1)-1) + (c[3]+u3[i]) * (X[i,j]-cp2) * (2*step(X[i,j]-cp2)-1)  + c[4] * X1_pa[i] + c[5] * X2_pa[i] + c[6] * X3_pa[i] + u[i]
        }
        for(j in 1:k.pe[i]){
  ## PE: recurrent event sub-model
       ## Weibull baseline
        lambda0[i,j] <- a*(Ti[i,j])^(a-1)
        lambda[i,j] <- lambda0[i,j]*v[i]*exp(b0+b[1]*X1[i]+b[2]*X2[i]+b[3]*X3[i])
       }
        u[i] ~ dnorm(0,u.tau)
        u1[i] ~ dnorm(0,u.tau1)
        u2[i] ~ dnorm(0,u.tau2)
        u3[i] ~ dnorm(0,u.tau3)
        L.a[i] <- prod(((p2[i,1:k.pa[i]])^(Y[i,1:k.pa[i]]))*((1-p2[i,1:k.pa[i]])^(1-Y[i,1:k.pa[i]])))
        ll.a[i] <- log(L.a[i])
        w[i] ~ dnorm(0,w.tau)
        v[i] <- exp(ga*u[i]+w[i]+ga1*u1[i]+ga2*u2[i]+ga3*u3[i])
        L.e[i] <- ifelse(Ti[i,1]!=0, prod(lambda[i,1:k.pe[i]]) * exp(v[i]*exp(b0+b[1]*X1[i]+b[2]*X2[i]+b[3]*X3[i])*(time.t0[i]^a-time.tau[i]^a)), exp(v[i]*exp(b0+b[1]*X1[i]+b[2]*X2[i]+b[3]*X3[i])*(time.t0[i]^a-time.tau[i]^a)))
        ll.e[i] <- log(L.e[i])
        phi[i] <- -log(L.e[i]) + 1000
        zeros[i] ~ dpois(phi[i])
  }
  log_lik0.a <- sum(ll.a[]) 
  log_lik0.e <- sum(ll.e[]) 
  dev.a <- -2*log_lik0.a
  dev.e <- -2*log_lik0.e

  ## priors for PA model
  c0 ~ dnorm(0,0.0001)
	for (k in 1:6){
	      c[k] ~ dnorm(0,0.0001)	
	}
	u.tau ~ dgamma(0.001,0.001)
	u.tau1 ~ dgamma(0.001,0.001)
	u.tau2 ~ dgamma(0.001,0.001)
	u.tau3 ~ dgamma(0.001,0.001)
	cp1 ~ dunif(0,max)
	cp2 ~ dunif(cp1,max)
	B1 <-c[1]-c[2]-c[3]
  B2 <-c[1]+c[2]-c[3]
  B3 <-c[1]+c[2]+c[3]
  u.tau.inv <- 1/u.tau  ## variance 
  u.tau1.inv <- 1/u.tau1  
  u.tau2.inv <- 1/u.tau2  
  u.tau3.inv <- 1/u.tau3  
  
  ## priors for PE model
  a ~ dgamma(0.01,0.01)
  b0 ~ dnorm(0,0.0001)	
  for (p in 1:3){
	     b[p] ~ dnorm(0,0.0001)		
  }
  
  ## priors for random effect structure
	ga ~ dnorm(0,0.0001)
	ga1 ~ dnorm(0,0.0001)
	ga2 ~ dnorm(0,0.0001)
	ga3 ~ dnorm(0,0.0001)
	w.tau ~ dgamma(0.001,0.001)
	w.tau.inv <- 1/w.tau  ## variance 
}"
